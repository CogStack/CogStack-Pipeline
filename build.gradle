plugins {
	// Apply the java plugin to add support for Java
	id 'java'
	id 'groovy'
	id 'idea'
	id 'org.springframework.boot' version "1.5.1.RELEASE"
}

repositories {
	mavenCentral()
	//needed for locally installed jdbc4 from microsoft
	mavenLocal()
	maven { url "https://artifacts.elastic.co/maven" }
	maven { url "https://maven.elasticsearch.org/releases" }
}

version '1.2.0'
sourceCompatibility = 1.8
targetCompatibility = 1.8

sourceSets {
	main {
		java { srcDirs = [] }
		groovy {
			srcDirs = ['src/main/java', 'src/main/groovy']
		}
		resources {
			srcDir 'src/main/resources'
		}

	}
}


if (!hasProperty('mainClass')) {
	ext.mainClass = 'uk.ac.kcl.Main'
}

jar {
	baseName = 'cogstack'
}

idea {
	module {
		downloadSources = true
	}
}


dependencies {
	//logging
	compile group: 'org.slf4j', name:'slf4j-api', version:'1.7.25'
	compile group: 'ch.qos.logback', name: 'logback-classic', version: '1.2.3'
	compile group: 'org.apache.logging.log4j', name: 'log4j-api', version: '2.11.1'
	compile group: 'org.apache.logging.log4j', name: 'log4j-core', version: '2.11.1'

	// misc
	compile group: 'joda-time', name: 'joda-time', version: '2.10.1'
	compile group: 'com.google.code.gson', name: 'gson', version: '2.8.5'
	compile group: 'commons-io', name: 'commons-io', version: '2.6'


	// elasticsearch rest client and apache asynch client
	compile group: 'org.elasticsearch.client', name: 'elasticsearch-rest-client', version: '6.4.2'
	compile group: 'org.apache.httpcomponents', name: 'httpasyncclient', version: '4.1.4'

	// elasticsearch x-pack support
	compile 'org.elasticsearch.client:x-pack-transport:6.4.2'

	// elasticsearch java client
	// deprecated -- will be removed in the next version with ElasticSearchDocumentWriter
	compile 'org.elasticsearch:elasticsearch:6.0.0'
	//

	// dbms drivers
	// compile("org.hsqldb:hsqldb")
	compile group: 'org.hsqldb', name: 'hsqldb', version: '2.4.1'
	compile group: 'com.microsoft.sqlserver', name: 'mssql-jdbc', version: '7.0.0.jre8'
	compile group: 'net.sourceforge.jtds', name: 'jtds', version: '1.3.1'
	compile group: 'mysql', name: 'mysql-connector-java', version: '8.0.13'
	compile group: 'org.postgresql', name: 'postgresql', version: '9.4-1205-jdbc42'

	// connection pool
	compile group: 'com.zaxxer', name: 'HikariCP', version: '2.4.6'


	// spring framework
	compile("org.springframework.boot:spring-boot-starter-batch")
	compile('org.springframework.batch:spring-batch-core:3.0.7.RELEASE')
	compile('org.springframework.batch:spring-batch-integration:3.0.7.RELEASE')
	compile('org.springframework:spring-web:4.3.6.RELEASE')
	compile('org.springframework:spring-core:4.3.6.RELEASE')
	compile('org.springframework:spring-test:4.3.6.RELEASE')
	compile('org.springframework:spring-jdbc:4.3.6.RELEASE')
	compile('org.springframework:spring-context:4.3.6.RELEASE')
	compile('org.springframework:spring-jms:4.3.6.RELEASE')
	compile('org.springframework.integration:spring-integration-core:4.3.6.RELEASE')
	compile('org.springframework.integration:spring-integration-jms:4.3.6.RELEASE')


	compile 'org.springframework.integration:spring-integration-java-dsl:1.1.2.RELEASE'
	compile 'org.apache.activemq:activemq-core:5.7.0'
	compile 'org.apache.activemq:activemq-spring:5.13.0'


	// nlp-related
	compile group: 'org.apache.commons', name: 'commons-collections4', version: '4.2'
	compile group: 'org.apache.commons', name: 'commons-lang3', version: '3.8.1'
	compile group: 'org.codehaus.groovy', name: 'groovy-all', version: '2.4.4' // --> 2.5.3

	// gate framework
	// deprecated -- any nlp applications will be run as separate services
	compile group: 'uk.ac.gate', name: 'gate-core', version: '8.4.1'
	compile group: 'xml-apis', name: 'xml-apis', version: '1.4.01'

	// tika
	compile 'org.apache.tika:tika-core:1.19.1' //
	compile('org.apache.tika:tika-parsers:1.19.1') {
		exclude group: 'commons-logging', module: 'commons-logging' //by both name and group
	}

	// temporary transitive dependencies fix
	compile 'org.json:json:20171018'

	compile group: 'org.apache.ws.commons.util', name: 'ws-commons-util', version: '1.0.2'

	//tests
	testCompile('org.mockito:mockito-all:1.10.19')
	testCompile group: 'de.sven-jacobs', name: 'loremipsum', version: '1.0'

}

task wrapper(type: Wrapper) {
	gradleVersion = '4.4'
	distributionType 'all'
}

allprojects {
	gradle.projectsEvaluated {
		tasks.withType(JavaCompile) {
			options.compilerArgs << "-Xlint:unchecked"
		}
	}
}

if (!hasProperty('profile')) {
	ext.profile = 'dev'
}

apply from: "gradle/profile.${profile}.gradle"